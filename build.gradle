import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
    }
}

plugins {
    id 'java'
    id 'findbugs'
    id 'org.springframework.boot' version '1.5.9.RELEASE'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

/***************************
 * build information
 ***************************/

springBoot {
    buildInfo {
        // Generate extra build info.
        additionalProperties = [
                by: System.properties['user.name'],
                time: buildTime(),
        ]
    }
}

// Display human readable build time, instead of epoch
def buildTime() {
    final dateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm:ssZ")
    dateFormat.format(new Date())
}

/***************************
 * gradle wrapper
 ***************************/
task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

/***************************
 * FindBugs
 ***************************/
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

/**
 * Unit Testing task, to avoid running slower integration tests
 */
task unitTest (type: Test) {
    exclude '**/*IntegrationTest.class'
}
